#!/usr/bin/env perl

use strict;
use warnings;
use Carp;

use MVC::Neaf;
$SIG{__WARN__} = \&Carp::cluck;

my $tpl = <<"TT";
<html>
<head>
	<title>[% title %]</title>
</head>
<body>
<h1>[% IF name %]Hello, [% name %]![% ELSE %]What's your name?[% END %]</h1>
<form method="POST" action="/change">
	Change name: <input name="name"/><input type="submit" value="&gt;&gt;"/>
</form>
</body>
</html>
TT

MVC::Neaf->route("/change" => sub {
	my $req = shift;

	die 403 unless $req->method eq 'POST';

	my $name = $req->param( name => qr/[-\w ]+/, '' );
	if (length $name) {
        $req->set_cookie( name => $name );
	};

	$req->redirect( "/" );
});

MVC::Neaf->route("/" => sub {
	my $req = shift;

	my $name = $req->get_cookie( name => qr/[-\w ]+/ );
	return {
		-view => 'TT',
		-template => \$tpl,
		title => 'Hello',
		name => $name,
	};
});

MVC::Neaf->run;

